// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library of basic system services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
    	/* Calls all the init function, then the main function and halt the
    	program */
	    do Memory.init();
    	do Keyboard.init();
    	do Math.init();
    	do Output.init();
    	do Screen.init();
    
    	do Main.main();
    	
    	do Sys.halt();
    	
    	return ;
    }

    /** Halts execution. */
    function void halt() {
   		while(true)
   		{
   		
   		}
   		
   		return ;
    }

    /** Waits approximately duration milliseconds and then returns. */
    function void wait(int duration) {
    	var int indexDuration, indexMilliseconds;
    	
    	let indexDuration = 0;
    	
    	/* ERR1 : Sys.wait - Duration must be positive */
    	if(duration < 0)
    	{
    		do Sys.error(1);
    	}
    	
    	while(indexDuration < duration)
    	{
    		let indexMilliseconds = 0;
    		
    		while(indexMilliseconds < 100)
    		{
    			let indexMilliseconds = indexMilliseconds + 1;
    		}
    	
    		let indexDuration = indexDuration + 1;
    	}
    
    	return ;
    }

    /** Prints the given error code in the form "ERR<errorCode>", and halts. */
    function void error(int errorCode) {
    	do Output.printString("ERR");
    	do Output.printInt(errorCode);
    	do Output.println();
    	
    	do Sys.halt();
    	
    	return ;
    }
}
